util = require \util
dirs = require \path
{Walk,Reloader,Paths} = require "./loader"
{signal} = require "./signal"
{async,handle} = require "../magic"
{Server} = require "../server/server"
{Log} = require "../log"
Sync = require \sync

class ControllerSupport
	(@action)->
	call: (action,args)->
		...
	renderJSON: ->
		status: 200
		\content-type : "application/json"
		body: [JSON.stringify it]
	render: (path, args = {})->
		if path not instanceof String
			[args,path] = [path or args,@id]
		for vpath,view in ..views
			base = with dirs
				@join (@dirname vpath),@basename vpath,@extname vpath
			if base is path
				return view.run args
		return [\fail]

exports.ControllerLoader = (dir)->
	out = new class
		reload: signal!
	for file of Walk dir
		Reloader file, handle (exp)->
			keys = for id,action in Paths exp
				out[id] = action
				action.support.id = id
			out.reload.fire keys
	return out

exports.Controller = (actions)->
	for r,action in actions
		props = {support: new ControllerSupport actions[r]} <<< action
		actions[r] = action.bind props.support
		actions[r] <<< props
	return actions

exports.Controller.views = (sys)->
	ControllerSupport.views = sys

exports.action = (spec,func)->
	[func,spec] = if spec instanceof Function then [spec,{}] else [func,spec]

	out = (...args)->
		args .= 0 if @@length is 1 and typeof @@0 is \object
		pass = {}
		for param,type in spec
			pass[param] = if args[param]? then that else args.shift!
		return func.call pass,this

	out.expects = spec
	return out