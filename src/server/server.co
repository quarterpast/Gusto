# server.co
# it's a server
Q = require \q
http = require \q-http
url = require \url
querystring = require \querystring
vm = require \vm
fs = require \fs
util = require \util
Sync = require \sync

{Router,NotFound} = require "./router"
{Loader} = require "../mvc/loader"
{Log} = require "../log"
{async,SyncPromise} = require "../magic"

class Timer
	(req)->
		@id="#{req.connection.remoteAddress} #{req.path}"
		@start = new Date
	end: ->
		@finish = new Date
		Log.log @id+": "+(@finish-@start)+"ms"

class exports.Server
	@error = false
	@hijack = (promise,err)->
		@error = err
		promise.then ~>
			@error = false

	serve: !(request)->
		time = new Timer request
		res = {}
		try
			get = url.parse request.url,true .query
			post = if request.method is \POST and request.headers."content-length" then
				querystring.parse SyncPromise request.body.read!
			else {}
			request <<< {get,post}
			found = false
			for route of Router.route request
				if route not instanceof NotFound
					found = true
					{action,params} = route
					break
			return status: 404,onclose: time.~end unless found

			res = action params
		catch
			Log.error e.message
			console.log e.stack
			res = body: [e.message] status: 500
		finally
			return {
				headers: "content-type":"text/html"
				status: 200
				onclose: time.~end
			} <<< if ..error and (
				"content-type" not in res@headers or
				res.headers."content-type" is "text/html"
			) then ..error
			else if \forEach in res then
				body: res
			else res
	->
		@server = http.Server @~serve
	listen: (port,host)->
		Log.log "listening on %s:#port",(host or "*")
		@server.listen port,host