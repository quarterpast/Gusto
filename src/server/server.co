# server.co
# it's a server
Q = require \q
http = require \q-http
url = require \url
querystring = require \querystring
vm = require \vm
fs = require \fs
util = require \util
Sync = require \sync

{Router,NotFound} = require "./router"
{Loader} = require "../mvc/loader"
{Log} = require "../log"
{async,SyncPromise} = require "../magic"

class Timer
	(req)->
		@id="#{req.connection.remoteAddress} #{req.path}"
		@start = new Date
	end: ->
		@finish = new Date
		Log.log @id+": "+(@finish-@start)+"ms"

class exports.Server
	serve: (request)->
		time = new Timer request
		# request's finished, do our thing
		get = url.parse request.url,true .query
		post = if request.method is \POST and request.headers."content-length" then
			querystring.parse SyncPromise request.body.read!
		else {}
		request <<< {get,post}
		found = false
		for route of Router.route request
			if route not instanceof NotFound
				found = true
				{action,params} = route
				break
		return status: 404,onclose: time.~end unless found

		res = action params

		{
			status: 200
			onclose: time.~end
		} <<< if \forEach in res then
			body: res
		else res
	error: (e,r)->
		if e
			Log.error e.message
			console.log e.stack

	->
		@server = http.Server @~serve
	listen: (port,host)->
		Log.log "listening on %s:#port",(host or "*")
		@server.listen port,host